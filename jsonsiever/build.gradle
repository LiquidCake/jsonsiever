plugins {
    id 'java-library'

    id 'maven-publish'
    id 'org.jreleaser' version '1.13.1'
}

group = 'io.github.liquidcake'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.springframework:spring-web:6.1.10'
    compileOnly 'org.springframework:spring-context:6.1.10'
    compileOnly 'org.apache.tomcat.embed:tomcat-embed-core:10.1.25'
    compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.17.1'
    compileOnly 'org.yaml:snakeyaml:2.2'
    compileOnly 'org.slf4j:slf4j-api:1.7.25'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'
    testImplementation 'org.slf4j:slf4j-api:1.7.25'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('jar') {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

// publishing to Maven Central using Jreleaser (https://jreleaser.org/guide/latest/examples/maven/maven-central.html)

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.liquidcake'
            artifactId = 'jsonsiever'

            from components.java

            pom {
                name = 'Jsonsiever'
                description = 'Json filtering library for Java/Spring'
                url = 'https://github.com/LiquidCake/jsonsiever'
                inceptionYear = '2024'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'liquidcake'
                        name = 'Arseniy'
                        url = 'https://github.com/liquidcake/'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/liquidcake/jsonsiever.git'
                    developerConnection = 'scm:git:ssh://github.com/liquidcake/jsonsiever.git'
                    url = 'https://github.com/liquidcake/jsonsiever'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    gitRootSearch = true
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}